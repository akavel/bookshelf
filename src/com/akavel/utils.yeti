module com.akavel.utils;

import android.view.View;
import android.view.View$OnClickListener;
import android.util.Log;

load yeti.lang.io;

{
	len array = map \1 array |> sum,
	inList elem array = (-1 != index elem array),

	viewById obj id is ~android.view.View -> number -> ~View    = obj #findViewById(id),
	
	setOnClickListener view listener is ~View -> (~View -> ()) -> () =
		view#setOnClickListener((
			class Click extends View$OnClickListener
				void onClick(View v)
					listener v;
			end;
			new Click())),

	iterateFilesRecursively dir is string -> list<string> = (
		dirList = listDirectory false;
		depthFirstSearch =
			\case of
			(f :: files) :: dirs:
				rest = files :: dirs;
				if f.directory? then
					dirList f.path
						|> (:: rest)
						|> depthFirstSearch
				else
					f.path :. \(depthFirstSearch rest)
				fi;
			[] :: dirs:
				depthFirstSearch dirs;
			_:
				[]
			esac;
		depthFirstSearch [dirList dir]
		),

	log = {
		d g s = _ = Log#d(g, s),
		i g s = _ = Log#i(g, s),
	},
}
