module com.akavel.bookshelf.pageselview;

import android.content.Context;
import android.util.AttributeSet;
import android.view.LayoutInflater;
import android.view.View;
import android.widget.Button;
import android.widget.LinearLayout;
import android.widget.ListView;
import android.widget.TextView;

load com.akavel.utils;

// http://stackoverflow.com/a/2679284/98528
getListItemView list
	is ~ListView -> ~View =
	list#getChildAt(list#getHeaderViewsCount());

class PageSelectorView(Context context, AttributeSet attrs) extends LinearLayout(context, attrs)
	var buttonUp is ~Button = () unsafely_as ~Button,
	var buttonDown is ~Button = () unsafely_as ~Button,
	
	findViews _ =
		buttonUp :=
			viewById this R$id#page_selector_view_up unsafely_as ~Button;
		buttonDown :=
			viewById this R$id#page_selector_view_down unsafely_as ~Button,
	
	setLabel current total =
		viewById this R$id#page_selector_view_info unsafely_as ~TextView
			#setText("\(current)/\(total)");
		this#invalidate(),
	
	inflate _ =
		_ = LayoutInflater
			#from(context)
			#inflate(R$layout#layout_page_selector_view, this),

	_ =
		inflate ();
		findViews ();
		(),
	
	itemsOnPage list is ~ListView -> number =
		hlist = list#getMeasuredHeight();
		hitem = (getListItemView list)#getMeasuredHeight(); //TODO: add divider height?
		hlist / hitem |> int,
	
	void link(ListView list)
		bu = buttonUp; // workaround for a bug in Yeti compiler resolving captures in below class
		bd = buttonDown;
		list#setOnScrollListener((class L extends android.widget.AbsListView$OnScrollListener
			void onScroll(android.widget.AbsListView v, int firstVisible, int visibleCount, int totalCount)
				empty = (visibleCount == 0) or (totalCount == 0);
				current = if empty then 1 else firstVisible + 1 fi;
				total = if empty then 1 else totalCount fi;
				setLabel current total;
				bu#setEnabled(current > 1);
				bd#setEnabled(current + visibleCount <= totalCount);
				(),
			void onScrollStateChanged(android.widget.AbsListView v, int scrollState)
				(),
			end; new L()));
		setOnClickListener bd do v:
			n = itemsOnPage list;
			i = list#getFirstVisiblePosition() + n
				|> min (list#getCount() - 1);
			i = list#getLastVisiblePosition();
			list#setSelectionFromTop(i, 0)
		done;
		setOnClickListener bu do v:
			n = itemsOnPage list;
			i = list#getFirstVisiblePosition() - n |> max 0;
			list#setSelectionFromTop(i, 0)
		done;
		//TODO: also add to listview's onDataChangedListeners
		(),


end;
