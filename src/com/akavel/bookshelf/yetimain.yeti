module com.akavel.bookshelf.yetimain;

import android.content.Context;
import android.database.Cursor;
import android.os.Bundle;
import android.support.v4.app.FragmentActivity;
import android.util.AttributeSet;
import android.view.LayoutInflater;
import android.view.Menu;
import android.view.View;
import android.view.ViewGroup;
import android.widget.BaseAdapter;
import android.widget.LinearLayout;
import android.widget.ListView;
import android.widget.TextView;

load com.akavel.bookshelf.scanbooks;
load com.akavel.utils;

class BookView(Book book, Context context, AttributeSet attrs) extends LinearLayout(context, attrs)
	self _ = this,
	setText id text is number -> string -> () = (viewById this id unsafely_as ~TextView)#setText(text),
	
	_ = (context#getSystemService(Context#LAYOUT_INFLATER_SERVICE) unsafely_as ~LayoutInflater)
			#inflate(R$layout#layout_book, self ());
		(self ()) #update(book),
	void update(Book book)
		if not nullptr? book then
			setText R$id#title book#title;
			setText R$id#author book#author;
			setText R$id#path book#path
		fi,
end;


class BooksDataAdapter(Cursor cursor) extends BaseAdapter
	var cursor = cursor,
	int getCount()
		cursor#getCount(),
	Object getItem(int pos)
		if cursor#moveToPosition(pos) then
			new Book(cursor#getString(0), cursor#getString(1), cursor#getString(2))
		fi,
	long getItemId(int pos)
		if cursor#moveToPosition(pos) then
			cursor#getLong(3)
		else
			-1
		fi,
	int getItemViewType(int pos) 0,
	View getView(int pos, View convertView, ViewGroup parent)
		book = (this#getItem(pos) unsafely_as ~Book);
		view = (convertView unsafely_as ~BookView);
		if nullptr? book then
			()
		elif nullptr? view then
			new BookView(book, parent#getContext(), ())
		else
			view#update(book);
			view
		fi,
	int getViewTypeCount() 1,
	boolean hasStableIds() true,
	boolean isEmpty()
		this#getCount() == 0,
	boolean areAllItemsEnabled() true,
	boolean isEnabled(int pos) true,
	
	void requery()
		_ = cursor#requery(),  //FIXME: this method is deprecated
end;

import com.akavel.bookshelf.ScanBooksTask$OnProgressListener;

class MainActivity extends FragmentActivity
	void onCreate(Bundle state)
		super#onCreate(state);
		this#setContentView(R$layout#activity_main);
		
		books = new BooksStorage(this#getBaseContext());
		
		cursor = books#openCursor(0);
		shelfAdapter = new BooksDataAdapter(cursor);
		shelf = (this#findViewById(R$id#shelf2) unsafely_as ~ListView);
		shelf#setAdapter(shelfAdapter);
		
		booksScan = scanBooksTask this do progress:
			shelfAdapter#requery();
			shelfAdapter#notifyDataSetChanged();
			title = this#getResources()#getString(R$string#app_name) ^ ": scanned \(progress) book(s)";
			this#setTitle(title)
		done;
		booksScan#execute([]);
		(),

	boolean onCreateOptionsMenu(Menu menu)
		this#getMenuInflater()#inflate(R$menu#main, menu);
		true,
end;
