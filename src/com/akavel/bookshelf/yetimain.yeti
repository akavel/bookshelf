module com.akavel.bookshelf.yetimain;

import android.content.ActivityNotFoundException;
import android.content.ComponentName;
import android.content.Context;
import android.content.Intent;
import android.database.Cursor;
import android.net.Uri;
import android.os.Bundle;
import android.support.v4.app.FragmentActivity;
import android.util.AttributeSet;
import android.view.Gravity;
import android.view.Menu;
import android.view.View;
import android.view.View$OnClickListener;
import android.view.ViewGroup;
import android.view.ViewGroup$LayoutParams;
import android.webkit.MimeTypeMap;
import android.widget.BaseAdapter;
import android.widget.Button;
import android.widget.LinearLayout;
import android.widget.LinearLayout$LayoutParams;
import android.widget.ListView;
import android.widget.TextView;
import android.R$style;
import android.graphics.Typeface;
import android.text.TextUtils$TruncateAt;

import java.io.File;
import java.net.URLConnection;

load com.akavel.bookshelf.scanbooks;
load com.akavel.bookshelf.layout;
load com.akavel.utils;

typedef layoutParam = Fixed number | FillParent () | WrapContent ();
layoutParams w h is layoutParam -> layoutParam -> ~ViewGroup$LayoutParams = (
	encode = \case of
		FillParent (): ViewGroup$LayoutParams#FILL_PARENT;
		WrapContent (): ViewGroup$LayoutParams#WRAP_CONTENT;
		Fixed n: n
		esac;
	new ViewGroup$LayoutParams(encode w, encode h)
	);

class BookView(Book book, Context context, AttributeSet attrs) extends LinearLayout(context, attrs)
	title = new TextView(context),
	author = new TextView(context),
	path = new TextView(context),
	button = new Button(context),

	singleLineEllipsize view e
		is ~TextView -> 'a -> () =
		view#setLines(1);
		view#setMaxLines(1);
		view#setSingleLine();
		view#setEllipsize(case e of
			End (): TextUtils$TruncateAt#END;
			Start (): TextUtils$TruncateAt#START
			esac);
		(),
	
	margin = context#getResources()
		#getDimensionPixelSize(R$dimen#activity_horizontal_margin),

	linearParams w h margin gravity =
		param = new LinearLayout$LayoutParams(layoutParams w h);
		param#setMargins(margin, 0, margin, 0);
		param#gravity := gravity;
		param,

	layout _ =
		sub = new MainLayout(context, ());
		subh = new MainLayout(context, ());
		subhv = new MainLayout(context, ());
		
		sub#setVertical();
		subh#setHorizontal();
		subhv#setVertical();
		
		title#setTextAppearance(context, R$style#TextAppearance_Large);
		title#setTypeface(Typeface#DEFAULT_BOLD);
		title#setEllipsize(TextUtils$TruncateAt#END);
		singleLineEllipsize title (End ());
		
		author#setTextAppearance(context, R$style#TextAppearance_Medium);
		author#setEllipsize(TextUtils$TruncateAt#END);
		singleLineEllipsize author (End ());
		
		path#setTextAppearance(context, R$style#TextAppearance_Small);
		path#setEllipsize(TextUtils$TruncateAt#START);
		path#setGravity(Gravity#RIGHT);
		singleLineEllipsize path (Start ());
		
		button#setText("(^)");
		
		this#addView(sub,
			linearParams (FillParent ()) (FillParent ()) margin Gravity#TOP);
		sub#addView(title, mainLayoutParams (Fixed ()));
		sub#addView(subh, mainLayoutParams (Fixed ()));
		subh#addView(subhv, mainLayoutParams (Shy ()));
		subh#addView(button, mainLayoutParams (Fixed ()));
		subhv#addView(author, mainLayoutParams (Fixed ()));
		subhv#addView(path, mainLayoutParams (Fixed ()));
		(),
	
	update book is ~Book -> () =
		if not nullptr? book then
			title#setText(book#title);
			author#setText(book#author);
			path#setText(book#path);
		fi,

	_ = 
		layout ();
		update book,

	void update(Book book)
		update book,
	
	void setOnButtonClickListener(View$OnClickListener l)
		button#setOnClickListener(l),

	void setOnTextClickListener(View$OnClickListener l)
		title#setOnClickListener(l);
		author#setOnClickListener(l);
		path#setOnClickListener(l),
end;

class OnBookListener
	abstract void onBookButton(Book book),
	abstract void onBookText(Book book)
end;
newBookListener onButton onText
	is (~Book -> ()) -> (~Book -> ()) -> ~OnBookListener = (
	class L extends OnBookListener
		void onBookButton(Book book)
			onButton book,
		void onBookText(Book book)
			onText book,
	end; new L());

class BooksDataAdapter(Cursor cursor) extends BaseAdapter
	var cursor = cursor,
	var bookListener = () unsafely_as ~OnBookListener,
	
	int getCount()
		cursor#getCount(),
	Object getItem(int pos)
		if cursor#moveToPosition(pos) then
			new Book(cursor#getString(0), cursor#getString(1), cursor#getString(2))
		fi,
	long getItemId(int pos)
		if cursor#moveToPosition(pos) then
			cursor#getLong(3)
		else
			-1
		fi,
	int getItemViewType(int pos) 0,
	View getView(int pos, View convertView, ViewGroup parent)
		book = (this#getItem(pos) unsafely_as ~Book);
		view = (convertView unsafely_as ~BookView);
		if nullptr? book then
			()
		else
			v = if nullptr? view then
				new BookView(book, parent#getContext(), ())
			else
				view#update(book);
				view
			fi;
			x = this;
			v#setOnButtonClickListener((class L extends View$OnClickListener
				void onClick(View v)
					if defined? bookListener then
						bookListener#onBookButton(book)
					fi
				end; new L()));
			v#setOnTextClickListener((class L extends View$OnClickListener
				void onClick(View v)
					if defined? bookListener then
						bookListener#onBookText(book)
					fi
				end; new L()));
			v
		fi,
	int getViewTypeCount() 1,
	boolean hasStableIds() true,
	boolean isEmpty()
		this#getCount() == 0,
	boolean areAllItemsEnabled() true,
	boolean isEnabled(int pos) true,
	
	void requery()
		_ = cursor#requery();  //FIXME: this method is deprecated
		this#notifyDataSetChanged(),
	void setOnBookListener(OnBookListener l)
		bookListener := l,
end;

(:-/) a b =
	if nullptr? a then
		b
	else
		a
	fi;

mimeType uri context = (
	sUri = (uri is ~Uri)#toString();
	ext = MimeTypeMap#getFileExtensionFromUrl(sUri);
	mime1 = (context is ~Context)#getContentResolver()#getType(uri);
	mime2 = MimeTypeMap#getSingleton()#getMimeTypeFromExtension(ext);
	mime3 = URLConnection#guessContentTypeFromName(sUri);
	log.d "Bookshelf" "mime1=\(mime1) mime2=\(mime2) mime3=\(mime3) ext=\(ext) sUri=\(sUri)";
	mime1 :-/ mime2 :-/ mime3);


class MainActivity extends FragmentActivity
	appName _ = this#getResources()#getString(R$string#app_name),
	
	void onCreate(Bundle state)
		super#onCreate(state);
		
		layout = new MainLayout(this, ());
		
		pages1 = new PageSelectorView(this, ());
		layout#addView(pages1, mainLayoutParams (Fixed ()));
		
		lifted = new ListView(this);
		layout#addView(lifted, mainLayoutParams (Greedy ()));

		tabs = new TabChoiceView(this, ());
		layout#addView(tabs, mainLayoutParams (Fixed ()));
		
		shelf = new ListView(this);
		layout#addView(shelf, mainLayoutParams (Shy ()));
		
		pages2 = new PageSelectorView(this, ());
		layout#addView(pages2, mainLayoutParams (Fixed ()));

		pages1#link(lifted);
		pages2#link(shelf);

		this#setContentView(layout, layoutParams (FillParent ()) (FillParent ()));
		
		storage = new BooksStorage(this#getBaseContext());
		// force upgrading the DB schema if necessary
		//FIXME(akavel): do we actually need this?
		db = storage#getWritableDatabase();
		db#close();

		shelfAdapter = new BooksDataAdapter(storage#openCursor(0));
		shelf#setAdapter(shelfAdapter);
		_ = (scanBooksTask this do progress:
			shelfAdapter#requery();
			this#setTitle("\(appName ()): scanned \(progress) book(s)")
		done) #execute([]);

		// I/ActivityManager(  796): Starting activity: Intent { act=android.intent.action.VIEW dat=file:///sdcard/Download/Listy z Rosji. Rosja w 1839 roku.epub typ=application/epub flg=0x34000000 cmp=com.bn.nook.reader.activities/.ReaderActivity (has extras) }

		// I/ActivityManager(  796): Starting activity: Intent { cmp=com.estrongs.android.pop/.view.OpenRecomm (has extras) }
		// I/ActivityManager(  796): Starting activity: Intent { act=android.intent.action.VIEW cat=[android.intent.category.DEFAULT] dat=file:///sdcard/downloads/FUTU.RE%20-%20fragment.epub typ=application/epub+zip flg=0x10000000 cmp=uk.co.droidinactu.ebooklauncher/.NookReaderActivity }
		// I/ActivityManager(  796): Starting activity: Intent { act=android.intent.action.VIEW cat=[android.intent.category.DEFAULT] dat=file:///sdcard/downloads/FUTU.RE%20-%20fragment.epub typ=application/epub+zip flg=0x10000000 cmp=com.bn.nook.reader.activities/.ReaderActivity }

		// FAIL:
		// I/ActivityManager(  796): Starting activity: Intent { act=android.intent.action.VIEW dat=file:///sdcard/downloads/FUTU.RE%20-%20fragment.epub }
		// E/Bookshelf(15325): cannot start activity: android.content.ActivityNotFoundException: No Activity found to handle Intent { act=android.intent.action.VIEW dat=file:///sdcard/downloads/FUTU.RE%20-%20fragment.epub }
		
		setBookListener view shelfTo
			is ~ListView -> number -> () = (
			adapter view =
				(view is ~ListView)#getAdapter() unsafely_as ~BooksDataAdapter;
			(adapter view)#setOnBookListener(newBookListener do book:
				log.d "ShelfListener" "book \(book) to \(shelfTo)";
				storage#moveBook(book, shelfTo);
				(adapter lifted)#requery();
				(adapter shelf)#requery();
				()
			done do book:
				uri = Uri#fromFile(new File(book#path));
				mime = mimeType uri this#getBaseContext();
				i = new Intent(Intent#ACTION_VIEW)
					#addCategory(Intent#CATEGORY_DEFAULT)
					#setFlags(Intent#FLAG_ACTIVITY_NEW_TASK)
					#setDataAndType(uri, mime);
				try
					log.d "Bookshelf" "starting activity: ((mime=\(mime))) \(i)";
					this#startActivity(i);
					storage#markOpened(book)
				catch ActivityNotFoundException ex:
					log.e "Bookshelf" "cannot start activity: ((mime=\(mime))) \(ex)";
					i = new Intent(Intent#ACTION_VIEW, uri)
						#setFlags(Intent#FLAG_ACTIVITY_NEW_TASK)
						#setComponent(new ComponentName("com.bn.nook.reader.activities", "com.bn.nook.reader.activities.ReaderActivity"));
					try
						log.d "Bookshelf" "starting explicit Nook Touch activity: ((mime=\(mime))) \(i)";
						this#startActivity(i);
						storage#markOpened(book)
					catch ActivityNotFoundException ex:
						log.e "Bookshelf" "cannot start explicit Nook Touch activity: \(ex)"
					yrt
				yrt
			done));
		
		tabs#addOnTabChangedListener((class L extends TabChoiceView$OnTabChangedListener
			void onTabChanged(int i)
				shelf#setAdapter(new BooksDataAdapter(storage#openCursor(i)));
				setBookListener shelf (0-1);
				setBookListener lifted i,
			end;
			new L()));
		
		lifted#setAdapter(new BooksDataAdapter(storage#openCursor(-1)));
		
		setBookListener shelf (0-1);
		setBookListener lifted 0;
		
		(),

	boolean onCreateOptionsMenu(Menu menu)
		this#getMenuInflater()#inflate(R$menu#main, menu);
		true,
end;
